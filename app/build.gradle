apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'deploygate'
apply plugin: 'com.cookpad.android.licensetools'

// Manifest version
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def packageName = "jp.gr.java_conf.miwax.troutoss"

// for debug info
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    if(project.hasProperty('devBuild')) {
        aaptOptions.cruncherEnabled = false
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion 15
        targetSdkVersion 25
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        resValue "string", "applicationName", packageName
        resValue "string", "versionName", versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
        }
        release {
            storeFile file("../keystore/release.keystore")
            storePassword System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
            minifyEnabled false
            multiDexEnabled true
            resValue "string", "applicationName", packageName + applicationIdSuffix
            resValue "string", "bannerAdUnitId", "ca-app-pub-3940256099942544/6300978111"
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled false
            resValue "string", "bannerAdUnitId", "ca-app-pub-3940256099942544/6300978111"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

deploygate {
    userName = System.getenv("DEPLOYGATE_USERNAME")
    token = System.getenv("DEPLOYGATE_TOKEN")

    apks {
        debug {
            // set as build message
            message = "debug build ${gitSha} at ${buildTime}"
        }

        release {
            // set as build message
            message = "release build ${gitSha} at ${buildTime}"
        }
    }
}

repositories {
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:0.14'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.14'
    kapt 'com.android.databinding:compiler:2.3.1'

    // UI
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:preference-v14:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.marshalchen.ultimaterecyclerview:library:0.7.2'
    compile 'com.makeramen:roundedimageview:2.3.0'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'
    compile 'com.google.android:flexbox:0.2.6'

    // Mastodon
    compile 'com.github.sys1yagi.mastodon4j:mastodon4j:1.0.0'
    compile 'com.github.sys1yagi.mastodon4j:mastodon4j-rx:1.0.0'

    // Rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "io.reactivex.rxjava2:rxjava:2.0.9"

    // Network
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0'

    // Util
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    compile 'com.uphyca:stetho_realm:2.0.0'

    // Debug
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.deploygate:sdk:3.1.1'

    // Firebase
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.google.firebase:firebase-ads:$firebaseVersion"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
